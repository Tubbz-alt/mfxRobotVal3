<?xml version="1.0" encoding="utf-8" ?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2" >
  <Program name="displayMessage7" access="private" >
    <Parameters xmlns="http://www.staubli.com/robotics/VAL3/Param/1" >
      <Parameter name="nTitle" type="num" use="value" xsi:type="element" dimensions="1" />
      <Parameter name="nKey" type="num" use="reference" xsi:type="array" dimensions="1" />
    </Parameters>
    <Locals>
      <Local name="LbKey" type="bool" xsi:type="array" size="1" />
      <Local name="LnFirst" type="num" xsi:type="array" size="1" />
      <Local name="LnIndex" type="num" xsi:type="array" size="1" />
      <Local name="LnQuestion" type="num" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  //header begin
  // program displayMessage7(nTitle, nKey)
  //
  // (this program modified from pgm displayMessage5( )
  // this program used specifically for pgm mnuEnterIPVals() because of added text displayed on UserPage()
  //
  // input parms:
  //    nTitle = index of selected menu Title string in sUserPageText[ ]
  //
  // other parms:
  //    nQuestion = nTitle+1 = index of selected menu Question string in sUserPageText[ ]
  //    nFirst = nTitle+2 = index of the first menu Items to be displayed from sUserPageText[ ]
  //
  //  NOTE:  the strings structure is in groups of 10 in the strings array sUserPageText[ ]
  //  Menu Page Indices are NUMBER variables
  //
  // output parms:
  //    nKey = the keypad number of the selected Function Key
  //
  // called from:  various
  // Added for Release 2:  KN
  //
  //header end
  //
  // init local variables used as menu string text pointers
  // values are just +1 & +2 offset of nTitle pointer
  LnQuestion=nTitle+1
  LnFirst=nTitle+2
  // clear user page, and throw a title
  userPage()
  cls()
  title(sUserPageText[nTitle])
  //
  // Print menu page Function Button Identifiers at bottom of user window
  gotoxy(0,13)
  put(sMenuBottomLine[(nTitle-1)/10])
  //
  //print QUESTION or COMMAND
  gotoxy(0,2)
  put(sUserPageText[LnQuestion])
  //
  // print 8 statements from menu string data
  for LnIndex=LnFirst to LnFirst+7
    gotoxy(0,LnIndex-LnFirst+3)
    put(sUserPageText[LnIndex])
  endFor
  //
  // populate the UserPage() with {X, Y, Z, RX, RY, RZ} values of the selected Interaction Point
  if nIPNumber==-1
    gotoxy(12,3)
    put(toString(".1",fIPoriginal.trsf.x))
    gotoxy(12,4)
    put(toString(".1",fIPoriginal.trsf.y))
    gotoxy(12,5)
    put(toString(".1",fIPoriginal.trsf.z))
    gotoxy(12,6)
    put(toString(".1",fIPoriginal.trsf.rx))
    gotoxy(12,7)
    put(toString(".1",fIPoriginal.trsf.ry))
    gotoxy(12,8)
    put(toString(".1",fIPoriginal.trsf.rz))
  elseIf nIPNumber==0
    gotoxy(12,3)
    put(toString(".1",fIP1.trsf.x))
    gotoxy(12,4)
    put(toString(".1",fIP1.trsf.y))
    gotoxy(12,5)
    put(toString(".1",fIP1.trsf.z))
    gotoxy(12,6)
    put(toString(".1",fIP1.trsf.rx))
    gotoxy(12,7)
    put(toString(".1",fIP1.trsf.ry))
    gotoxy(12,8)
    put(toString(".1",fIP1.trsf.rz))
  else
    gotoxy(12,3)
    put(toString(".1",fIP2.trsf.x))
    gotoxy(12,4)
    put(toString(".1",fIP2.trsf.y))
    gotoxy(12,5)
    put(toString(".1",fIP2.trsf.z))
    gotoxy(12,6)
    put(toString(".1",fIP2.trsf.rx))
    gotoxy(12,7)
    put(toString(".1",fIP2.trsf.ry))
    gotoxy(12,8)
    put(toString(".1",fIP2.trsf.rz))
  endIf
  // Wait indefinitely for a function button press
  LbKey=false
  getKey()
  do
    nKey=getKey()
    switch nKey
      // ESC key always ABORTS AND / OR RETURNS
      case 255
        LbKey=true
      break
      // Function Button value returned for selected action
      case 271,272,273,274,275,276,277,278
        LbKey=true
      break
    endSwitch
    //
    delay(0)
  until (LbKey==true)
end]]></Code>
  </Program>
</Programs>