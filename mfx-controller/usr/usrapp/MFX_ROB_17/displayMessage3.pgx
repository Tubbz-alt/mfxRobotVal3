<?xml version="1.0" encoding="utf-8" ?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2" >
  <Program name="displayMessage3" access="private" >
    <Parameters xmlns="http://www.staubli.com/robotics/VAL3/Param/1" >
      <Parameter name="nTitle" type="num" use="value" xsi:type="element" dimensions="1" />
      <Parameter name="nKey" type="num" use="reference" xsi:type="array" dimensions="1" />
      <Parameter name="XnDeltaRX" type="num" use="value" xsi:type="element" dimensions="1" />
      <Parameter name="XnDeltaRY" type="num" use="value" xsi:type="element" dimensions="1" />
      <Parameter name="XnDeltaRZ" type="num" use="value" xsi:type="element" dimensions="1" />
    </Parameters>
    <Locals>
      <Local name="LbKey" type="bool" xsi:type="array" size="1" />
      <Local name="LnFirst" type="num" xsi:type="array" size="1" />
      <Local name="LnIndex" type="num" xsi:type="array" size="1" />
      <Local name="LnQuestion" type="num" xsi:type="array" size="1" />
      <Local name="LnYoffset" type="num" xsi:type="array" size="1" />
      <Local name="LtrDisplay" type="trsf" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  //header begin
  // program displayMessage3(nTitle, nKey)
  //
  // generic pgm used for displaying complex messages and reacting to user keypad selections
  // (this program modified from pgm display2( )
  //  CUSTOMIZED SPECIFICALLY FOR TARGET FRAME / TARGET POINT ADJUSTMENT OF RX, RY, RZ
  //
  // input parms:
  //    nTitle = index of selected menu Title string in sUserPageText[ ]
  //
  //  NOTE:  the strings structure is in groups of 10 in the strings array sUserPageText[ ]
  //  Menu Page Indices are NUMBER variables
  //
  // output parms:
  //    nKey = the keypad number of the selected Function Key
  //
  // called from:  pgm mnuAdjTargetPnt()
  //
  // initial release KN
  //header end
  //
  // init local variables used as menu string text pointers
  // values are just +1 & +2 offset of nTitle pointer
  // LnYoffset used for pendant display positioning
  LnQuestion=nTitle+1
  LnFirst=nTitle+2
  LnYoffset=4
  // clear user page, and throw a title
  userPage()
  cls()
  title(sUserPageText[nTitle])
  //
  // print the Function Button Identifiers
  // at bottom of user window
  gotoxy(0,13)
  switch nSelection
    case 1
      put("Sel. Chng ---- ---- ---- ----      Ret.")
    break
    case 2,3,4
      put("Sel. ---- Inc. Dec. ---- Mov.      Ret.")
    break
  endSwitch
  //
  gotoxy(2,0)
  put("RX RY RZ adjustments applied")
  gotoxy(2,1)
  put("to current TCP location")
  gotoxy(2,3)
  put("Point data NOT SAVED on EXIT")
  //gotoxy(1,3)
  //put(sUserPageText[nTitle+3])
  //gotoxy(1,4)
  //put(sUserPageText[nTitle+4])
  gotoxy(1,5)
  put(sUserPageText[nTitle+5])
  gotoxy(1,6)
  put(sUserPageText[nTitle+6])
  gotoxy(1,7)
  put(sUserPageText[nTitle+7])
  gotoxy(1,8)
  put(sUserPageText[nTitle+8])
  gotoxy(1,9)
  put(sUserPageText[nTitle+9])
  // print the currently selected resolution
  gotoxy(18,5)
  put(toString("6.3",nResolution[nIdxResolution]))
  gotoxy(26,5)
  put("degrees")
  //
  gotoxy(18,6)
  // print adjustment value on current position's RX
  put(toString(".3",XnDeltaRX))
  // print adjustment value on current position's RY
  gotoxy(18,7)
  put(toString(".3",XnDeltaRY))
  // print adjustment value on current position's RZ
  gotoxy(18,8)
  put(toString(".3",XnDeltaRZ))
  //
  // put brackets around default selected menu item
  // nSelection is global & set in pgm mnuAdjTargetPnt()
  gotoxy(0,nSelection+LnYoffset)
  put("[>")
  gotoxy(14,nSelection+LnYoffset)
  put("<]")
  // Wait indefinitely for a function button press
  LbKey=false
  getKey()
  do
    nKey=getKey()
    switch nKey
      // ESC character always ABORTS AND/OR RETURNS
      case 255
        LbKey=true
      break
      // move selection brackets down the displayed list
      case 271
        // overwrite with blanks any previous brackets [> <]
        // offset pendant display Y location based on nSelection
        for LnIndex=(nSelection+LnYoffset) to (nSelection+LnYoffset+4)
          gotoxy(0,LnIndex)
          put("  ")
          gotoxy(14,LnIndex)
          put("  ")
        endFor
        // on F1="Sel." keypress, increment the menu item selector for bracket [> <] placement
        nSelection=nSelection+1
        if nSelection==5
          nSelection=1
        endIf
        // put [> <] around the menu item selection text
        switch nSelection
          // case=3, select "Resolution"
          case 1
            gotoxy(0,nSelection+LnYoffset)
            put("[>")
            gotoxy(14,nSelection+LnYoffset)
            put("<]")
          break
          //case=4, select "Target RX"
          case 2
            gotoxy(0,nSelection+LnYoffset)
            put("[>")
            gotoxy(14,nSelection+LnYoffset)
            put("<]")
          break
          // case=5, select "Target RY"
          case 3
            gotoxy(0,nSelection+LnYoffset)
            put("[>")
            gotoxy(14,nSelection+LnYoffset)
            put("<]")
          break
          // case=6, select "RZ"
          case 4
            gotoxy(0,nSelection+LnYoffset)
            put("[>")
            gotoxy(14,nSelection+LnYoffset)
            put("<]")
          break
          default
            popUpMsg("Bad Selection...Press OK")
          break
        endSwitch
        // print the Function Button Identifiers
        // at bottom of user window for new menu item text selection
        gotoxy(0,13)
        switch nSelection
          case 1
            put("Sel. Chng ---- ---- ---- ----      Ret.")
            //bEnableIncDec=false
          break
          case 2,3,4
            put("Sel. ---- Inc. Dec. ---- Mov.      Ret.")
          break
        endSwitch
      break
      // any other keypress, send the key num back to pgm mnuAdjTargetPnt() for changes
      case 272,273,274,276,278
        LbKey=true
      break
    endSwitch
    //
    delay(0)
  until (LbKey==true)
  //
  //done
  //
end]]></Code>
  </Program>
</Programs>